plugins {
    id 'java'
    id 'jacoco'
    id 'pmd'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
}

version = '1.0.0'

project.ext {
    dropwizardVersion = '0.8.1'
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-jdbi:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-assets:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-views-mustache:' + dropwizardVersion

    testCompile "junit:junit:4.12"
    testCompile 'io.dropwizard:dropwizard-testing:' + dropwizardVersion
    testCompile 'org.seleniumhq.selenium:selenium-java:2.45.0'
}

sourceSets {
    acceptanceTest {
        java.srcDir file('src/acceptanceTest/java')
        resources.srcDir file('src/acceptanceTest/resources')
        compileClasspath += main.output + test.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task acceptanceTest(type: Test) {
    description = 'Runs the acceptance tests'
    group = 'verification'
    testClassesDir = sourceSets.acceptanceTest.output.classesDir
    classpath = sourceSets.acceptanceTest.runtimeClasspath
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    manifest {
        attributes 'Main-Class': 'se.thinkcode.Main',
                'Implementation-Version': version
    }
}

repositories {
    jcenter()
}

pmd {
    toolVersion = '5.1.3'
    ignoreFailures = true
    ruleSets = [
            'java-basic',
            'java-unusedcode'
    ]
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
